apply plugin: 'maven-publish'
apply plugin: 'signing'

publishing {
    publications {
        all {
            pom {
                url = 'https://github.com/xenit-eu/xenit-testcontainers'
                name = project.name
                description = "Extension on top of https://www.testcontainers.org/, making it easy to startup Alfresco or Alfred containers in JUnit tests."

                scm {
                    connection = 'scm:git:git@github.com:xenit-eu/xenit-testcontainers.git'
                    developerConnection = 'scm:git:git@github.com:xenit-eu/xenit-testcontainers.git'
                    url = 'https://github.com/xenit-eu/xenit-testcontainers.git'
                }
                developers {
                    developer {
                        id = "xenit"
                        name = "Xenit Solutions NV"
                    }
                }

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
            }
        }
    }

    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

            credentials {
                username = project.hasProperty('de_publish_username') ? project.de_publish_username : ''
                password = project.hasProperty('de_publish_password') ? project.de_publish_password : ''
            }
        }
    }
}

afterEvaluate {
    signing {
        required { !version.endsWith('SNAPSHOT') && gradle.taskGraph.hasTask("publish") }
        sign publishing.publications
    }
}

gradle.taskGraph.whenReady { graph ->
    if (graph.hasTask(publish)) {
        if (!project.hasProperty('keyId') || !project.hasProperty('secretKeyRingFile') || !project.hasProperty('password')) {
            throw new GradleException('You need to provide signing params in order to sign artifacts')
        }

        def id = project.hasProperty('keyId') ? project.keyId : ''
        def file = project.hasProperty('secretKeyRingFile') ? project.secretKeyRingFile : ''
        def password = project.hasProperty('password') ? project.password : ''

        ext."signing.keyId" = id
        ext."signing.secretKeyRingFile" = file
        ext."signing.password" = password
    }
}